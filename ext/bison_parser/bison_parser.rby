
%token IDENTIFIER
%token STRING
%token COLON
%token SEMICOLON
%token LBRACK
%token RBRACK
%token PIPE
%token HASH
%token DOUBLE_HASH
%token KW_TOKEN
%token ACTIONS

%%

grammar_file :
  token_list[tokens] DOUBLE_HASH grammar_rules[rules] optional_code[code]
  { self.result = Bison::GrammarFile.new(tokens, rules, code) }
;

optional_code :
  { nil }
|
  DOUBLE_HASH ACTIONS[actions]
  { actions }
;

token_list :
  { [] }
|
  token_list[list]
  HASH KW_TOKEN IDENTIFIER[name]
  { list << name }
;

grammar_rules:
  { [] }
|
  grammar_rules[list] grammar_rule[rule]
  { list << rule }
;

grammar_rule:
  IDENTIFIER[name] COLON components[components] SEMICOLON
  { Bison::Rule.new(name, components) }
;

components:
  { [] }
| component[component]
  { [component] }
|
  components[sequence] PIPE component[component]
  { sequence << component }
;


component:
  sequence
|
  sequence[sequence] ACTIONS[action]
  { sequence.tap{ |s| s.action = action } }
;

sequence:
  { Bison::Sequence.new }
|
  sequence[sequence] IDENTIFIER[follower]
  { sequence << Bison::Nonterminal.new(follower) }
|
  sequence[sequence] IDENTIFIER[follower] LBRACK IDENTIFIER[tag] RBRACK
  { sequence << Bison::Nonterminal.new(follower, tag) }
;

%%

class BisonParser
  attr_accessor :section

  def error(msg, row, col)
    abort("#{row}.#{col}: #{msg}")
  end

  def lex
    self.section ||= 0
    self.lex_value = nil

    if section == 2
      self.lex_value = io.read
      self.section += 2
      return Tokens::ACTIONS
    end

    # skip space
    while (c = self.read) && c =~ /\s/
    end
    
    return nil unless c

    case c
    when ':'
      return Tokens::COLON
    when ';'
      return Tokens::SEMICOLON
    when '|'
      return Tokens::PIPE
    when '%'
      if self.peak == '%'
        self.read
        self.section += 1
        return Tokens::DOUBLE_HASH
      end
      return Tokens::HASH
    when '['
      return Tokens::LBRACK
    when ']'
      return Tokens::RBRACK
    when '{'
      nesting = 1
      action = ''
      while (c = self.read) && nesting > 0
        nesting += 1 if c == '{'
        nesting -= 1 if c == '}'
        action << c unless nesting.zero?
      end
      self.lex_value = action
      return Tokens::ACTIONS
    end

    if c =~ /\w/
      string = c
      while (c = self.peak) && c =~ /\w/
        self.read
        string << c
      end

      case string
      when 'token'
        return Tokens::KW_TOKEN
      else
        self.lex_value = string
        return Tokens::IDENTIFIER
      end
    end

    return nil
  end
end
